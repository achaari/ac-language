.include "acdluti.h" 

/* CmplProc prototypes */
.statement_block;
.variable_declaration;
.function_definition;
.expression;

/* Type definition, exmpl : Object, Package::Object, NameSpace::Package::Object */
.type_name : {ident ["::" *] } ;

/* variable/member reference or function call */
.variable_name   : ident ['(' [')' ;] { <expressions> [',' *] } ')' ] ; 

.unit_expression : 
    /* primary expression : literal or ( expression ) or variable */
    { [literal ,] ['(' <expression> ')' ,] <variable_name> }   
    
    /* Extend access */
    { [ %("->", "::", ":", ".")  <variable_name> * ] }
    
    /* expression postfix */ 
    [ %("++", "--") ]    

    /* Type cast */
    [~as <type_name> ]    
    
    /* affectation operator */
    [ %("=", "+=", "-=", "*=", "/=", "&=", "|=") <expression> ] 
    
    /* arithmetic or logic operator */
    [ %("||", "&&", "|", "&", "==", ">=", "<=", ">>", "<<", ">", "<" "!=", "+", "-", "*", "/", "^") <expression> ] 
    ;
    
.expression : 
    [ ~(typeof, sizeof) '(' <type_name> ')' ;]
    [ %("++", "--") <expression> ; ]  /* expression Prefix  */
    [ %("!", "-", "+", "&", "*", "~", "$", "@") <unit_expression> 
    [ ~(is, extend) <type_name> ]
    [ '?' <expressions> ':' <expression> ]
    ;

.expression_statement : 
    [ ';' ; ]                    /* No opration statement */
    [ ':' ident ;]               /* label defition (goto) */
    [ ~(break,continue) ';' ;]   
    [ ~goto ident ';' ;]
    [ ~return [<expression>] ';' ;]
    [ <variable_declaration> ;]
    <expression> ';' 
    ;

.block_statement : 
    [ <statement_block> ;]
    [ ~if { '(' <expression> ')' <block_statement> [~else *] } ;] 
    [ ~while '(' <expression> ')' <block_statement> ;]
    [ ~do <block_statement> ~while '(' <expression> ')' ';' ;] 
    [ ~switch '(' <expression> ')' '{' { ['}' ,] [~default ':' <block_statement> '}' ,] ~case literal ':' <block_statement> * } ;]
    [ ~for '(' <expression_statement> { [';' ,] <expression>  ';'} {[')' ,] <expression> ')' } <block_statement> ;]
    [ ~foreach '(' <expression> ~in <expression> [ ':' <expression> ] ')' } <block_statement> ;]
    <expression_statement> 
    ;
    
.statement_block : '{' { ['}' ,] <block_statement> * } ;

.variable_definition : ident [ ~as <type_name>] [ '=' <expression> ] ;

.object_member : 
    [ ~(public, private, protected, internal, extern, sealed, static, virtual, const)] 
    [ ~var <variable_definition> ';' ;]
    <function_definition> ;

/* CmplProc entry-point */
.variable_declaration : ~(local,global) <variable_definition> [',' <variable_definition> ;] ';' ;
.function_definition  : ~function ident '(' { [')' ,] { ident [')' ,] ',' * } } [';' ;] <statement_block> ;
.object_definition    : ~object ident [':' { <type_name> [',' *] } ] '{' { <object_member> ['}' ';' ,] * } ;

/* Main CmplProc */
.main : 
    [<variable_declaration> ;]
    [<object_definition>    ;]
    [<function_definition>  ;] 
    ;
